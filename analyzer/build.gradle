// Apply core plugins.
apply plugin: 'application'

applicationName = 'analyzer'
mainClassName = 'com.here.ort.analyzer.Main'

sourceSets {
    funTest {
        kotlin.srcDirs 'src/funTest/kotlin'
    }
}

dependencies {
    compile project(':downloader')
    compile project(':model')
    compile project(':util')

    compile "com.beust:jcommander:$jcommanderVersion"
    compile "com.vdurmont:semver4j:$semverVersion"

    compile "org.apache.maven:maven-core:$mavenVersion"
    compile "org.apache.maven:maven-compat:$mavenVersion"

    // The classes from the maven-resolver dependencies are not used directly but initialized by the Plexus IoC
    // container automatically. They are required on the classpath for Maven dependency resolution to work.
    compile "org.apache.maven.resolver:maven-resolver-connector-basic:$mavenResolverVersion"
    compile "org.apache.maven.resolver:maven-resolver-transport-http:$mavenResolverVersion"

    compile "org.gradle:gradle-tooling-api:${gradle.gradleVersion}"

    funTestCompile sourceSets.main.output
    funTestCompile sourceSets.test.output
    funTestCompile configurations.testCompile
    funTestRuntime configurations.testRuntime
    funTestCompile "io.kotlintest:kotlintest:$kotlintestVersion"
}

task funTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'Verification'
    classpath = sourceSets.funTest.runtimeClasspath
    testClassesDirs = sourceSets.funTest.output.classesDirs

    testLogging {
        events 'started', 'passed', 'skipped', 'failed'
        exceptionFormat 'full'
    }
}

task funTestCoverage(type: JacocoReport, dependsOn: funTest) {
    description = 'Generates the code coverage report for the functional tests.'
    group = 'Reporting'

    executionData funTest
    sourceSets sourceSets.main

    reports {
        // Enable XML in addition to HTML for CI integration.
        xml.enabled true
    }
}

check.dependsOn funTest
