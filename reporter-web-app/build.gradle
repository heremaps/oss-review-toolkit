plugins {
    id 'com.moowork.node' version '1.2.0'
}

node {
    version = '8.11.4'
    // Version 1.12.1 of Yarn does not work on AppVeyor and gives
    //
    //     Failed to capture fingerprint of output files for task ':reporter-web-app:install' property '$1' during up-to-date check.
    //     > Could not read path 'reporter-web-app\node_modules\@babel\generator\node_modules\jsesc\.bin\jsesc'.
    //
    // Maybe this is because of https://github.com/gradle/gradle/issues/1365.
    yarnVersion = '1.9.4'
    // Installing NPM is needed for installing Yarn because the Gradle Node plugin installs Yarn via NPM.
    npmVersion = '6.4.0'
    // Setting download flag is required for bootstrapping NPM.
    download = true
    nodeModulesDir = file("${project.projectDir}/node")
}

task clean(type: Delete) {
    delete 'build'
    delete 'node_modules'
    delete 'yarn-error.log'
}

task install(type: YarnTask) {
    description 'Use Yarn to install the node dependencies. Similar to the "yarn" task but with up-to-date checks.'
    group = 'Node'
    args = ['install']

    inputs.files 'package.json', 'yarn.lock'

    outputs.dir 'node_modules'
}

task build(type: YarnTask, dependsOn: install) {
    description 'Use Yarn to build the package. Similar to the "yarn_build" task but with up-to-date checks.'
    group = 'Node'
    args = ['build']

    inputs.dir 'config'
    inputs.dir 'node_modules'
    inputs.dir 'public'
    inputs.dir 'scripts'
    inputs.dir 'src'

    outputs.dir 'build'
}

task check(dependsOn: [build, yarn_lint])
