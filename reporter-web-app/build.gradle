plugins {
    id "com.moowork.node" version "1.2.0"
}

node {
    version = "8.11.4"
    yarnVersion = "1.9.4"

    nodeModulesDir = file("${project.projectDir}/node")
}

task install(type: YarnTask) {
    description 'Run yarn install to install the node dependencies. Similar to the yarn task but with up-to-date ' +
            'checks.'
    group = 'Node'
    args = ['install']

    inputs.file 'package.json'
    inputs.file 'yarn.lock'

    outputs.dir 'node_modules'
}

task build(type: YarnTask, dependsOn: install) {
    description 'Run yarn build to build the package. Similar to the yarn_build task but with up-to-date checks.'
    group = 'Node'
    args = ['build']

    inputs.dir 'config'
    inputs.dir 'node_modules'
    inputs.dir 'public'
    inputs.dir 'scripts'
    inputs.dir 'src'

    outputs.dir 'build'
}
