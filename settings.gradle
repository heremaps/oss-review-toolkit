pluginManagement {
    resolutionStrategy {
        eachPlugin {
            // Work around https://github.com/gradle/gradle/issues/1697.
            if (requested.version == null) {
                def versionPropertyName = (requested.id.id == 'org.jetbrains.kotlin.jvm') ?
                        "kotlinVersion" : "${requested.id.name}Version"
                println("Checking for plugin version property '$versionPropertyName'.")
                if (gradle.rootProject.hasProperty(versionPropertyName)) {
                    def version = gradle.rootProject.properties[versionPropertyName]
                    println("Setting '${requested.id.id}' plugin version to $version.")
                    useVersion version
                } else {
                    println("Property '$versionPropertyName' does not exist.")
                }
            }
        }
    }
}

rootProject.name = 'oss-review-toolkit'

include 'analyzer'
include 'cli'
include 'downloader'
include 'model'
include 'reporter'
include 'reporter-web-app'
include 'scanner'
include 'utils'
include 'utils-test'
