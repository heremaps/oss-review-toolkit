plugins {
    // Apply core plugins.
    id 'java-library'
}

dependencies {
    api project(':spdx-utils')

    api "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

    implementation project(':utils')

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    implementation "com.vdurmont:semver4j:$semverVersion"

    implementation 'org.jetbrains.kotlin:kotlin-reflect'
}

task generateVersionResource {
    group = 'Build'
    description = 'Generates a plain text resource file containing the current application version.'

    def git = grgit.open(dir: rootDir)
    def version = git.describe(longDescr: true, tags: true) ?: git.head().abbreviatedId
    def versionFile = file("$projectDir/src/main/resources/VERSION")

    inputs.property('version', version)
    outputs.file(versionFile)

    doLast {
        versionFile.text = version
    }
}

compileKotlin.dependsOn(generateVersionResource)

idea.project.settings {
    taskTriggers {
        afterSync tasks.getByName('generateVersionResource')
    }
}
