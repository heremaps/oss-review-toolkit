// Apply core plugins.
apply plugin: 'java-library'

dependencies {
    api "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

    implementation project(':utils')

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    implementation "com.vdurmont:semver4j:$semverVersion"
}

task generateSpdxLicenseList {
    doLast {
        println "Fetching SPDX license list..."
        def jsonSlurper = new groovy.json.JsonSlurper()
        def spdxUrl = "https://raw.githubusercontent.com/spdx/license-list-data/master/json/licenses.json".toURL()
        def json = jsonSlurper.parse(spdxUrl)
        def spdxLicenseIds = json.licenses.collect {
            it["licenseId"]
        }

        println "Found ${spdxLicenseIds.size()} SPDX license identifiers."

        def licenseEnumFile = file('src/main/kotlin/config/SpdxLicense.kt')
        licenseEnumFile.write("""
            /*
             * Copyright (C) 2017-2018 HERE Europe B.V.
             *
             * Licensed under the Apache License, Version 2.0 (the "License");
             * you may not use this file except in compliance with the License.
             * You may obtain a copy of the License at
             *
             *     http://www.apache.org/licenses/LICENSE-2.0
             *
             * Unless required by applicable law or agreed to in writing, software
             * distributed under the License is distributed on an "AS IS" BASIS,
             * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
             * See the License for the specific language governing permissions and
             * limitations under the License.
             *
             * SPDX-License-Identifier: Apache-2.0
             * License-Filename: LICENSE
             */

            package com.here.ort.model.config

            /**
             * An enum containing all SPDX license IDs. This class is generated by the Gradle task
             * 'generateSpdxLicenseList'.
             */
            enum class SpdxLicense(val license: String) {
            """.stripIndent())
        licenseEnumFile.append(spdxLicenseIds.collect { "    ${licenseToEnumEntry(it.toString())}" }.sort().join(',\n'))
        licenseEnumFile.append("""
            }
            """.stripIndent())
        println("Generated SPDX license enum '$licenseEnumFile'.")
    }
}

def licenseToEnumEntry(String license) {
    def enumName = license.toUpperCase().replaceAll('[-\\.]', '_').replace('+', 'PLUS')
    if (enumName.startsWith('0')) {
        enumName = "_$enumName"
    }
    return "$enumName(\"$license\")"
}
